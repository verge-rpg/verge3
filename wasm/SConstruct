import os
import os.path
import sys

# Find emcc + others
path = os.environ['PATH'].split(os.path.pathsep)

def locate_command(path, command):
    for p in path:
        p2 = os.path.join(p, command)
        if os.path.exists(p2):
            return p2

    print('Could not find ' + command +'.  Check your PATH?')
    sys.exit(1)

emcc = locate_command(path, 'emcc')
emar = locate_command(path, 'emar')
emranlib = locate_command(path, 'emranlib')

def EmscriptenEnvironment():
    if sys.platform in ('windows', 'win32'):
        env_dict = {
            'path': os.environ['PATH']
        }

        for key in ['HOME', 'USERPROFILE', 'EM_CONFIG', 'EMSDK_PYTHON', 'EMSDK_NODE']:
            value = os.environ.get(key)
            if value is not None:
                env_dict[key] = value

        env = Environment(ENV=env_dict, TOOLS=['mingw'])

        # ar on Windows omits ranlib, we need to put it back.
        env['ARCOM'] = '$AR $ARFLAGS $TARGET $SOURCES\n$RANLIB $RANLIBFLAGS $TARGET'
    else:
        env = Environment()

    env['AR'] = emar
    env['RANLIB'] = emranlib
    env['CC'] = emcc
    env['CXX'] = emcc

    emscriptenCommonOpts = [
        '-s', 'USE_ZLIB=1',
        '-s', 'USE_SDL=2',
        '-s', 'USE_GIFLIB=1',
        '-s', 'USE_LIBPNG=1',
        '-s', 'USE_LIBJPEG=1',
    ]    
    emscriptenCompilerOpts = []
    emscriptenLinkerOpts = [
        '-s', 'ASYNCIFY',
        '-s', 'ASYNCIFY_IMPORTS=["fetchSync","downloadAll","wasm_nextFrame","emscripten_sleep","wasm_initSound","wasm_loadSound","wasm_getSongPosition"]',
        '-s', 'FETCH=1',
        '-s', 'FORCE_FILESYSTEM=1',
        '-s', 'ALLOW_MEMORY_GROWTH=1',
        '-s', 'LLD_REPORT_UNDEFINED',
        '-s', 'NO_DISABLE_EXCEPTION_CATCHING',
    ]

    cflags = ['-fcolor-diagnostics', '-fexceptions']

    asmjs = ARGUMENTS.get('asmjs', 0)
    debug = int(ARGUMENTS.get('debug', 0))
    asan = ARGUMENTS.get('asan', 0)
    ubsan = ARGUMENTS.get('ubsan', 0)
    cyberdwarf = ARGUMENTS.get('cyberdwarf', 0)

    debug_flags = {
        'debug_functions',
        'debug_locals',
        'debug_input',
        'debug_vc',
        'profile_vc'
    }
    
    for flag in debug_flags:
        if ARGUMENTS.get(flag, 0) > 0:
            env.Append(CPPDEFINES=[
                flag.upper()
            ])

    if debug:
        if not asan:
            emscriptenLinkerOpts += [
                '-s', 'SAFE_HEAP=1',
            ]

        emscriptenLinkerOpts += [
            '-s', 'ASYNCIFY_STACK_SIZE=327680',
            '-s', 'ASSERTIONS=2',
            '-s', 'STACK_OVERFLOW_CHECK=1',
            '-s', 'DEMANGLE_SUPPORT=1',
        ]

        cflags.append('-g')
        cflags.append('-DVERGE_EMSCRIPTEN_DEBUG')
        cflags.append('-DCORONA_DEBUG')

        env.Append(LINKFLAGS=[
            '-gsource-map',
            '--source-map-base', 'http://localhost:8000/wasm/',
        ])

    else:
        emscriptenLinkerOpts += [
            '-s', 'ASYNCIFY_STACK_SIZE=32768',
        ]
        cflags.append('-O3')
        # cflags.append('-flto')
        # env.Append(LINKFLAGS=['-flto'])

    if cyberdwarf:
        env.Append(LINKFLAGS=[
            '-s', 'CYBERDWARF=1'
        ])

    if asmjs:
        env.Append(LINKFLAGS=[
            '-s', 'WASM=0',
        ])

    if asan:
        cflags.append('-fsanitize=address')
        env.Append(LINKFLAGS=['-fsanitize=address'])
    if ubsan:
        cflags.append('-fsanitize=undefined')
        env.Append(LINKFLAGS=['-fsanitize=undefined'])

    cflags.extend([
        '-MMD',
        '-Wno-parentheses',
        '-Wno-long-long',
        '-Wno-dangling-else',
        '-Wno-writable-strings',
    ])

    cflags.extend(emscriptenCommonOpts + emscriptenCompilerOpts)

    env.Append(CFLAGS=cflags)
    env.Append(CXXFLAGS=cflags)

    env.Append(LINKFLAGS=[
        '-lidbfs.js',
    ] + emscriptenCommonOpts + emscriptenLinkerOpts)

    return env

verge_dir = '../verge/Source'
verge_sources = [verge_dir + '/' + s for s in [
    'algebra3.cpp',
    'a_common.cpp',
    'a_dict.cpp',
    'a_vfile.cpp',
    'a_codec.cpp',
    'a_config.cpp',
    'a_handle.cpp',
    'a_image.cpp',
    'a_string.cpp',
    'g_script.cpp',
    'g_startup.cpp',
    'g_chr.cpp',
    'g_font.cpp',
    'g_controls.cpp',
    'g_engine.cpp',
    'g_editcode.cpp',
    'g_sound.cpp',
    'snd_wasm.cpp',
    'g_entity.cpp',
    'g_sprites.cpp',
    'g_map.cpp',
    'g_vsp.cpp',
    'garlick.cpp',
    'lua_main.cpp',
    'lua_vector.cpp',
    'vc_builtins.cpp',
    'vc_core.cpp',
    'vc_compiler.cpp',
    'vc_library.cpp',
    'vc_debug.cpp',
    'vid_manager.cpp',
    'vid_sysfont.cpp',
    'vid_ddblit.cpp',
    'vid_timeless.cpp',
    'vid_fbfx.cpp',
    'vid_macbase.cpp',
    'mac_joystick.cpp',
    'mac_network.cpp',
    'mac_keyboard.cpp',
    'mac_mouse.cpp',
    'mac_system.cpp',
    'wasm_timer.cpp',
    'mac_movie.cpp',
    'xerxes.cpp',
]]

# TODO: make this optional and have separate Lua and non-Lua builds? (for VC games that don't need it)
lua_dir = '../lua/lua-5.1.2'
lua_sources = [lua_dir + '/' + s for s in [
    'lapi.c',
    'lauxlib.c',
    'lbaselib.c',
    'lcode.c',
    'ldblib.c',
    'ldebug.c',
    'ldo.c',
    'ldump.c',
    'lfunc.c',
    'lgc.c',
    'linit.c',
    'liolib.c',
    'llex.c',
    'lmathlib.c',
    'lmem.c',
    'loadlib.c',
    'lobject.c',
    'lopcodes.c',
    'loslib.c',
    'lparser.c',
    'lstate.c',
    'lstring.c',
    'lstrlib.c',
    'ltable.c',
    'ltablib.c',
    'ltm.c',
    'lundump.c',
    'lvm.c',
    'lzio.c',
    'print.c',
]]

# TODO: could this be replaced with SDL_Image?
corona_dir = '../corona'
corona_sources = [corona_dir + '/' + s for s in [
    'Corona.cpp',
    'Convert.cpp',
    'Debug.cpp',
    'DefaultFileSystem.cpp',
    'MemoryFile.cpp',
    'OpenBMP.cpp',
    'OpenGIF.cpp',
    'OpenPCX.cpp',
    'OpenPNG.cpp',
    'OpenJPEG.cpp',
    'OpenTGA.cpp',
    'SavePNG.cpp',
    'SaveTGA.cpp',
]]

verge_cpppath = [
    verge_dir,
    lua_dir,
    corona_dir,
    #zlib_dir,
    #libungif_dir,
    #libpng_dir,
    #libjpeg_dir
]

verge_libs = [
    'lua',
    'corona',
]

env = EmscriptenEnvironment()

env.Append(
    CXXFLAGS=[
        '-std=c++17',
        #'-Werror', # Hahaha no way.  This code dates back to like 2001
    ],
    CPPPATH=verge_cpppath
)

lua = env.Library('lua', lua_sources)
corona = env.Library('corona', corona_sources)
verge = env.Program('verge3.out.js', verge_sources, LIBS=verge_libs, LIBPATH='.', PROGSUFFIX='.js')